" set the runtime path to include fzf
set rtp+=~/.fzf

call plug#begin('~/.local/share/nvim/plugged')

" disable ale lsp
let g:ale_disable_lsp = 1

" ----------------
" Plugins
" ----------------

" Utilities
" inline git status
Plug 'airblade/vim-gitgutter'
" Git for vim
Plug 'tpope/vim-fugitive'
" Github gist
Plug 'mattn/webapi-vim'
Plug 'mattn/gist-vim'
" Gbrowse for files or lines
Plug 'tpope/vim-rhubarb'
" code completion
Plug 'neoclide/coc.nvim', {'branch': 'release'}
" Bookmark lines
Plug 'MattesGroeger/vim-bookmarks'
" FZF file search
Plug 'junegunn/fzf', { 'dir': '~/.fzf', 'do': './install --all' }
Plug 'junegunn/fzf.vim'
" comment files with keyboard shortcuts
Plug 'tpope/vim-commentary'
" UNIX file commands
Plug 'tpope/vim-eunuch'
" :e any/non/existant/path.txt to create it
Plug 'pbrisbin/vim-mkdir'
" recognize .editorconfig files
Plug 'editorconfig/editorconfig-vim'
" Linter for all the things
Plug 'w0rp/ale'
" Rake in vim
Plug 'tpope/vim-rake'
" Rails commands and rails-specific syntax
Plug 'tpope/vim-rails'
" auto-add 'end' and 'endif'
Plug 'tpope/vim-endwise'
" Ruby syntax
Plug 'vim-ruby/vim-ruby'
" CoffeeScript syntax
Plug 'kchmck/vim-coffee-script'
" ES6 syntax and snippets
Plug 'isRuslan/vim-es6'
" Slim
Plug 'slim-template/vim-slim'
" Crystal
Plug 'rhysd/vim-crystal'
" VIML tests
Plug 'junegunn/vader.vim'
" Distraction free mode
Plug 'junegunn/goyo.vim'
" Show me the whitespace
Plug 'ntpeters/vim-better-whitespace'
" Golang
Plug 'fatih/vim-go'
" autoclose braces, brackets, etc
Plug 'Townk/vim-autoclose'
" alignment
Plug 'junegunn/vim-easy-align'
" Dockerfiles
Plug 'ekalinin/Dockerfile.vim'
" TOML
Plug 'cespare/vim-toml'
" JavaScript
Plug 'pangloss/vim-javascript'
Plug 'leafgarland/typescript-vim'
Plug 'peitalin/vim-jsx-typescript'
Plug 'styled-components/vim-styled-components', { 'branch': 'main' }

" -------------
" Color Pallete
" -------------
Plug 'w0ng/vim-hybrid'

" Initialize plugin system
call plug#end()


" Colors and Syntax
syntax on
filetype plugin indent on    " required
if (has("termguicolors"))
  set termguicolors
endif
colorscheme hybrid
set background=dark
let g:hybrid_custom_term_colors = 1
let g:hybrid_reduced_contrast = 0 " Remove this line if using the default palette.
set guifont="IBM Plex Mono":h13
set colorcolumn=0

:set tags=./tags;

" Syntax for files

autocmd BufNewFile,BufRead Jenkinsfile set filetype=groovy shiftwidth=4 tabstop=4
autocmd FileType groovy set shiftwidth=4 tabstop=4
autocmd BufNewFile,BufRead *.pp set filetype=ruby
autocmd BufNewFile,BufRead *.pp set syntax=ruby
autocmd FileType go set shiftwidth=4 tabstop=4

autocmd BufEnter *.{js,jsx,ts,tsx} :syntax sync fromstart
autocmd BufLeave *.{js,jsx,ts,tsx} :syntax sync clear

augroup FiletypeGroup
    autocmd!
    au BufNewFile,BufRead *.jsx set filetype=javascript.jsx
augroup END

set updatetime=500
set nocursorline
set expandtab
set backspace=2
set modelines=0
set shiftwidth=2
set clipboard=unnamed
set synmaxcol=0
if !has('nvim')
  set ttyscroll=10
endif
set encoding=utf-8
set tabstop=2
set wrap
set number
set expandtab
set nowritebackup
set noswapfile
set nobackup
set hlsearch
set ignorecase
set smartcase
set showtabline=2
set noshowmode
set noruler
set noshowcmd
set hidden

" splits
set splitright " open vsplit on the right
set splitbelow " open split below current

" set directory for undofiles
" allows for history after a file has been closed
set undofile
set undodir=~/.vim/undodir

" Tab between buffers
noremap <tab> <c-w><c-w>
" Use FZF for buffer navigation
nnoremap <silent> <leader>b :Buffers<CR>

nmap <C-]> :tabnext<CR>
nmap <C-[> :tabprevious<CR>
nunmap <Esc>

" bind ctrl-p to FZF File browsers
map <C-p> :Files <enter>

" Remove Ex keymap
map Q <Nop>

" Remove command history keymap
nnoremap q: <nop>

au FileType qf call AdjustWindowHeight(3, 10)
function! AdjustWindowHeight(minheight, maxheight)
  exe max([min([line("$"), a:maxheight]), a:minheight]) . "wincmd _"
endfunction

" open error loclist
let g:ale_open_list = 1
" Dont check as text is changed
let g:ale_lint_on_text_changed = 0
" Dont run on enter
let g:ale_lint_on_enter = 0
" Fix on save
let g:ale_fix_on_save = 1
" disable ale lsp
let g:ale_disable_lsp = 1

" ui colors for statusline
hi TabLine      gui=none ctermfg=242 ctermbg=233 cterm=none
hi TabLineSel   gui=bold ctermfg=14  ctermbg=233 cterm=bold
hi TabLineFill  gui=none ctermfg=242 ctermbg=233 cterm=none
let g:currentmode={
	\ 'n'  : 'Normal',
	\ 'no' : 'N·Operator Pending',
	\ 'v'  : 'Visual',
	\ 'V'  : 'V·Line',
	\ '' : 'V·Block',
	\ 's'  : 'Select',
	\ 'S'  : 'S·Line',
	\ '' : 'S·Block',
	\ 'i'  : 'Insert',
	\ 'R'  : 'Replace',
	\ 'Rv' : 'V·Replace',
	\ 'c'  : 'Command',
	\ 'cv' : 'Vim Ex',
	\ 'ce' : 'Ex',
	\ 'r'  : 'Prompt',
	\ 'rm' : 'More',
	\ 'r?' : 'Confirm',
	\ '!'  : 'Shell',
  \ 't'  : ''
\}

function! DrawInsertHighlight()
  hi! statusline gui=none ctermfg=14 ctermbg=233
endfunction

function! DrawNormalHighlight()
  hi! statusline gui=none ctermfg=233 ctermbg=250
endfunction

function! DrawReplaceHighlight()
  hi! statusline gui=none ctermfg=1 ctermbg=233
endfunction

function! DrawVisualHighlight()
  hi! statusline gui=none ctermfg=11 ctermbg=233
endfunction

" change statusline background color for modes
function! ChangeStatuslineColors()
  if (mode() =~# '\v(n|no)')
    call DrawNormalHighlight()
  elseif (mode() =~# '\v(v|V|^V)' || g:currentmode[mode()] ==# 'V·Block')
    call DrawVisualHighlight()
  elseif (mode() ==# 'i')
    call DrawInsertHighlight()
  elseif (mode() =~# '\v(R|Rv)' || g:currentmode[mode()] ==# 'V·Replace')
    call DrawReplaceHighlight()
  else
    call DrawNormalHighlight()
  endif

  return ''
endfunction

" Formats the statusline
let &stl  = ''
let &stl .= '%{ChangeStatuslineColors()}'
let &stl .= '[%{toupper(mode())}]'
let &stl .= ' '      " Separator
let &stl .= '%f'     " file name
let &stl .= '%m'     " modified flag
let &stl .= '%r'     " read only flag

" Git info
let &stl .= ' '
let &stl .= '%{fugitive#statusline()} '   " Buffer's Git info

" Linter info
function! LinterStatus() abort
    let l:counts = ale#statusline#Count(bufnr(''))

    let l:all_errors = l:counts.error + l:counts.style_error
    let l:all_non_errors = l:counts.total - l:all_errors

    return l:counts.total == 0 ? 'OK' : printf(
    \   '%dW %dE',
    \   all_non_errors,
    \   all_errors
    \)
endfunction

let &stl.= '%#warningmsg#'
let &stl .= " %{LinterStatus()} "
let &stl .= '%*'

" Coc-vim settings

" Global extensions
let g:coc_global_extensions = [
  \ 'coc-go',
  \ 'coc-phpls',
  \ 'coc-tsserver',
  \ 'coc-json',
  \ 'coc-markdownlint',
  \ 'coc-solargraph',
  \ 'coc-yaml',
  \ 'coc-css',
  \ 'coc-html',
  \ ]

if isdirectory('./node_modules') && isdirectory('./node_modules/prettier')
  let g:coc_global_extensions += ['coc-prettier']
endif

if isdirectory('./node_modules') && isdirectory('./node_modules/eslint')
  let g:coc_global_extensions += ['coc-eslint']
endif

" Show docs on hover
function! ShowDocIfNoDiagnostic(timer_id)
  if (coc#util#has_float() == 0)
    silent call CocActionAsync('doHover')
  endif
endfunction

" function! s:show_hover_doc()
"   call timer_start(500, 'ShowDocIfNoDiagnostic')
" endfunction

" Use K to show documentation in preview window.
nnoremap <silent> K :call <SID>show_documentation()<CR>

" autocmd CursorHoldI * :call <SID>show_hover_doc()
" autocmd CursorHold * :call <SID>show_hover_doc()

" Remap keys for gotos
nmap <silent> gd <Plug>(coc-definition)
nmap <silent> gy <Plug>(coc-type-definition)
nmap <silent> gi <Plug>(coc-implementation)
nmap <silent> gr <Plug>(coc-references)

" Navigate between errors
nmap <silent> [g <Plug>(coc-diagnostic-prev)
nmap <silent> ]g <Plug>(coc-diagnostic-next)

" Show list of diagnostics
nnoremap <silent> <space>d :<C-u>CocList diagnostics<cr>

" Show symbols list
nnoremap <silent> <space>s :<C-u>CocList -I symbols<cr>

" Invoke code actions
nmap <leader>do <Plug>(coc-codeaction)

function! s:show_documentation()
  if (index(['vim','help'], &filetype) >= 0)
    execute 'h '.expand('<cword>')
  else
    call CocAction('doHover')
  endif
endfunction

" Highlight the symbol and its references when holding the cursor.
autocmd CursorHold * silent call CocActionAsync('highlight')

" Symbol renaming.
nmap <leader>rn <Plug>(coc-rename)

if filereadable(glob("~/.vimrc.local"))
  source ~/.vimrc.local
endif

set mouse=a
